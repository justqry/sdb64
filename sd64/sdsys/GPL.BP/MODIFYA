* MODIFYA
* MODIFY.ACCOUNT verb
* Add / Delete user from group account
* Copyright (c) String Database
*
* This program is free software; you can redistribute it and/or modify
* it under the terms of the GNU General Public License as published by
* the Free Software Foundation; either version 3, or (at your option)
* any later version.
* 
* This program is distributed in the hope that it will be useful,
* but WITHOUT ANY WARRANTY; without even the implied warranty of
* MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
* GNU General Public License for more details.
* 
* You should have received a copy of the GNU General Public License
* along with this program; if not, write to the Free Software Foundation,
* Inc., 59 Temple Place - Suite 330, Boston, MA 02111-1307, USA.
* 
*
* START-HISTORY:
* rev 0.9.0 Jan 25 mab initial release
* END-HISTORY
*
* START-DESCRIPTION:
* MODIFY.ACCOUNT account <ADD/DELETE> user.name
*
* END-DESCRIPTION
*
* START-CODE

$internal
program $modify.account
$catalog $MODIFYA

$include keys.h
$include int$keys.h
$include parser.h
$include err.h
* 
deffun is_grp_member(user,group) calling '!is_grp_member'
deffun is_group(group) calling '!is_group'

   prompt ''

   if not(kernel(K$ADMINISTRATOR, -1)) then
      stop sysmsg(2001)  ;* Command requires administrator privileges
   end     

   parser = "!PARSER"

   @system.return.code = -ER$ARGS     ;* Preset for argument errors
   
   openpath @sdsys:@ds:'ACCOUNTS' to acc.f else
      @system.return.code = -status()
      stop sysmsg(6000) ;* Cannot open ACCOUNTS file in SDSYS account
   end

   call @parser(PARSER$RESET, 0, @sentence, 0)
   
   call @parser(PARSER$GET.TOKEN, token.type, token, keyword) ;* Verb modify.Account

   * Get next token, account name
   call @parser(PARSER$GET.TOKEN, token.type, acc.name, keyword)
   if token.type = PARSER$END then
      crt 'Command Syntax:  MODIFY.ACCOUNT <ACCOUNT> <ADD/DELETE> <USERNAME> '
      stop
   end
   *
   acc.name = upcase(acc.name)   ;* account names are always upper case
   *
   read acc.rec from acc.f, acc.name else      
      stop sysmsg(6003) ;* Account name is invalid
   end
   
   group.name = acc.rec<ACC$GROUP>   ;* get associated group
   
   if is_group(group.name) then
      * Get next token, ADD or DELETE
      call @parser(PARSER$GET.TOKEN, token.type, action, keyword)
      if (upcase(action) = 'ADD') or (upcase(action) = 'DELETE') then
      *  valid action
      *   Get next token, user name
         call @parser(PARSER$GET.TOKEN, token.type, user.name, keyword) 
         if token.type = PARSER$END then
            crt ' Missing User Name '
            stop
         end
         
         if (upcase(action) = 'ADD') then
            * Add user to group        
            * does user belong to sdusers?
            if is_grp_member(user.name,'sdusers') then
            * this user looks legit, add to group
            * first check to see if user is already a member of the group
               if is_grp_member(user.name,group.name) THEN
                  crt user.name:' is already a member of ':group.name
               end else
                  os.execute "sudo usermod -aG ":group.name:" ":user.name
                  stat = OS.ERROR()
                  if stat = 0 then
                     crt user.name : ' added to group ': group.name
                  end else
                     crt 'unable to add user ': user.name : ' to group ' :  group.name :' status:  ':stat
                  end
               end 
            end else
               crt user.name: ' is not a member of sdsys, not allowed'
            end 
         
         end else
            * already tested action, must be delete
            if is_grp_member(user.name,group.name) THEN
               os.execute "sudo deluser ":user.name:" ":group.name
               stat = OS.ERROR()
                  if stat = 0 then
                     crt user.name : ' removed from group ': group.name
                  end else
                     crt 'unable to remove user ':user.name: ' from group ' :group.name:' status:  ':stat
                  end
            end else
               crt user.name: ' is not a member of  ' : group.name
            end 
          
         end
         
         
      end else
         crt 'MODIFY.ACCOUNT Action Must Be Add or Delete'
      end 
   
   end else
     crt group.name : ' is not a valid Linux Group'
   end
   END
